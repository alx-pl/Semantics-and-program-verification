{-

This file contains the grammar of TINY plus explanations how to render them
in BNFC.

-}

-- BNFC expressions that define the part of the grammar for statements
-- S ::= x := e | skip | S1; S2 | if b then S1 else S2 | while b do S

SSeq.      Stmt ::= Stmt1 ";" Stmt; -- right associative, execution RtoL
--SSeq.      Stmt ::= Stmt ";" Stmt1; -- left associative, execution LtoR
SAssgn.    Stmt1 ::= Ident ":=" Expr;
SSkip.     Stmt1 ::= "skip";
SIf.       Stmt2 ::= "if" BExpr "then" Stmt "else" Stmt1;

-- We use a function to implement omitting of the "else" branch.
sift.      Stmt2 ::= "if" BExpr "then" Stmt1;
-- However, this introduces a conflict which is due to the fact that 
-- after "then" we have both Stmt and Stmt1.

SWhile.    Stmt2 ::= "while" BExpr "do" Stmt1;

_. Stmt ::= Stmt1;
_. Stmt1 ::= Stmt2;
_. Stmt2 ::= "(" Stmt ")";

define sift b1 s1 = SIf b1 s1 SSkip; 

-- BNFC expressions that define the part of the grammar for boolean expressions
-- b ::= true | false | e1 ≤ e2 | ¬b | b1 ∧ b2

BAnd.  BExpr ::= BExpr "∧" BExpr1;
band.  BExpr ::= BExpr "/\\" BExpr1;
BLeq.   BExpr1 ::= Expr "<=" Expr;
bleq.   BExpr1 ::= Expr "≤" Expr;
BNot.   BExpr1 ::= "~" BExpr1;
bnot.   BExpr1 ::= "¬" BExpr1; 
BTrue.  BExpr1 ::= "true";
BFalse. BExpr1 ::= "false";

_.       BExpr1 ::= "(" BExpr ")";
_.       BExpr ::= BExpr1;

define band b1 b2 = BAnd b1 b2;
define bleq e1 e2 = BLeq e1 e2;
define bnot b1 = BNot b1;


-- BNFC expressions that define the part of the grammar for expressions
-- e ::= N | x | e1 + e2 | e1 ∗ e2 | e1 − e2

EPlus.   Expr  ::= Expr "+" Expr1;
EMinus.  Expr  ::= Expr "-" Expr1;

EMul.    Expr1 ::= Expr1 "*" Expr2; 
ENum.    Expr2 ::= Integer;
EVar.    Expr2 ::= Ident;

_.       Expr2 ::= "(" Expr ")"; -- We silently introduce parentheses 
_.       Expr1  ::= Expr2;       -- We silently make Expr1 and Expr2 equal
_.       Expr ::= Expr1;         -- We silently make Expr and Expr1 equal


